plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'net.ltgt.errorprone' version '1.2.1'
}

group 'me.hugmanrique'
version '1.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

ext {
    errorproneVersion = '2.4.0'
    junitVersion = '5.6.0'
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    implementation "com.google.errorprone:error_prone_annotations:${errorproneVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
}

javadoc {
    options.encoding = 'UTF-8'
    options.source = '11'
    options.links(
            'https://docs.oracle.com/en/java/javase/11/docs/api/'
    )

    // Disable Java 8+ doclint tool
    options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
    required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') }
    sign publishing.publications
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = 'Riviere'
                    description = 'Concurrent Stream builders'
                    url = 'https://github.com/hugmanrique/Riviere'

                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://raw.githubusercontent.com/hugmanrique/Riviere/main/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'hugmanrique'
                            name = 'Hugo Manrique'
                            email = 'maven@hugmanrique.me'
                        }
                    }

                    scm {
                        connection = 'scm:git@github.com:hugmanrique/Riviere.git'
                        developerConnection = 'scm:git@github.com:hugmanrique/Riviere.git'
                        url = 'https://github.com/hugmanrique/Riviere'
                    }
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}
